##Builds WinClient program
import os, sys, string
import SConsAddons.Util as sca_util
pj = os.path.join

Import('baseEnv ves_pkg LIBDIR GetPlatform buildDir xerces_options wxwidgets_options PREFIX RootDir vesSVNRevision')

srcs = Split("""
   ConductorApp.cxx 
   Avail_Modules.cxx
   AppFrame.cxx 
   Network.cxx 
   DeviceProperties.cxx 
   NavigationPane.cxx 
   PluginLoader.cxx 
   ViewLocPane.cxx
   UserPreferences.cxx
   Splitter.cxx
   ../DefaultPlugin/DefaultPlugin.cxx
   ../DefaultPlugin/DefaultPlugin_UI_Dialog.cxx
   MainToolBar.cxx
   UI_TeacherTab.cxx
   ExportMenu.cxx
""")

env = ves_pkg.getEnv().Copy()
env.Append( CPPDEFINES = [vesSVNRevision] )
env.Append(LIBS = ['VE_ConductorLib', 'GUIPlugin', 'VE_XML','VE_Conductor_Utilities',
                   'VE_OpenModuleLib'])
env.Append(LIBPATH = [pj('#', buildDir, 'VE_Conductor','Network'),
                        pj('#', buildDir, 'VE_Conductor','Utilities'),
                        pj('#', buildDir, 'VE_Conductor','GUIPlugin'),
                        pj('#', buildDir, 'VE_Open','skel'),
                        pj('#', buildDir, 'VE_Open','XML')])

env.ParseConfig('flagpoll TAO ACE TAO_IORInterceptor TAO_ObjRefTemplate TAO_Valuetype \
                              TAO_CosNaming TAO_Svc_Utils TAO_IORTable TAO_Messaging \
                              TAO_PortableServer TAO_BiDirGIOP TAO_AnyTypeCode \
                              TAO_Messaging TAO_Valuetype vpr --cflags --libs')
xerces_options.apply( env )
wxwidgets_options.apply( env )
prog = ves_pkg.createProgram('WinClient', env, 'bin')
prog.addSources( srcs )
prog.build()

if GetPlatform() == "darwin":
   idlCmd = '/Developer/Tools/Rez -d __DARWIN__ -t APPL Carbon.r -o ${SOURCE}'
   bld = Builder(action = idlCmd )
   env.Append(BUILDERS = {'apple_rez': bld})
   if 'install' in COMMAND_LINE_TARGETS:
      conductorTarget = pj(PREFIX,'bin','Conductor')
      winTarget = pj(PREFIX,'bin','WinClient')
   else:
      conductorTarget = pj( '#', buildDir,'VE_Conductor','Framework','Conductor')
      winTarget = pj( '#',buildDir,'VE_Conductor','Framework','WinClient')

   env.apple_rez( conductorTarget, winTarget)
   env.AddPostAction( conductorTarget,Copy('$TARGET','$SOURCE'))
