##Builds WinClient program
import os, sys, string
import SConsAddons.Util as sca_util
pj = os.path.join

Import('baseEnv ves_pkg LIBDIR GetPlatform buildDir xerces_options wxwidgets_options PREFIX RootDir GetSVNVersion')
Import('tao_options vrjuggler_options')

srcDir = os.path.abspath( pj(RootDir, 'src', 'apps', 'conductor') )
srcs = sca_util.getSourcesRecursive( srcDir )
headers = sca_util.getHeadersRecursive( srcDir )
#srcs += [pj( '..', '..', 'ves','conductor', 'DefaultPlugin', 'DefaultPlugin.cxx' )]
#srcs += [pj( '..', '..', 'ves','conductor', 'DefaultPlugin', 'DefaultPlugin_UI_Dialog.cxx' )]

env = ves_pkg.getEnv().Copy()
env.Append( CPPDEFINES = [ GetSVNVersion( srcDir ) ] )
env.Append(LIBS = ['ves_conductor_plugin', 'ves_open_xml',
                   'ves_open_module'])
env.Append(LIBPATH = [
                        pj('#', buildDir, 'src','ves','conductor'),
                        pj('#', buildDir, 'src','ves','open'),
                        pj('#', buildDir, 'src','ves','open','xml')
                     ])

xerces_options.apply( env )
wxwidgets_options.apply( env )
tao_options.apply( env )
vrjuggler_options.apply( env )

if GetPlatform() == 'win32':
   env.Append( CPPDEFINES = ['LOKI_DLL'])
   
prog = ves_pkg.createProgram('ves_conductor' + env["PROGSUFFIX"], env, 'bin')
prog.addSources( srcs )
prog.addHeaders( headers )
prog.build()

if GetPlatform() == "darwin":
   idlCmd = '/Developer/Tools/Rez -d __DARWIN__ -t APPL Carbon.r -o ${SOURCE}'
   bld = Builder(action = idlCmd )
   env.Append(BUILDERS = {'apple_rez': bld})
   if 'install' in COMMAND_LINE_TARGETS:
      conductorTarget = pj(PREFIX,'bin','Conductor')
      winTarget = pj(PREFIX,'bin','ves_conductor' + env["PROGSUFFIX"])
   else:
      conductorTarget = pj( '#', buildDir,'src', 'apps', 'conductor','Conductor')
      winTarget = pj( '#',buildDir,'src', 'apps', 'conductor','ves_conductor' + env["PROGSUFFIX"])

   env.apple_rez( conductorTarget, winTarget)
   env.AddPostAction( conductorTarget,Copy('$TARGET','$SOURCE'))
