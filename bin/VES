#!/usr/bin/tcsh -f

# Set environment variables and paths for running the program
source /isv/VE_Suite/20041118/VE_Installer/setup.deere.tsh
# Set additional environment variables
setenv MY_VJCONFIGS /isv/Juggler/deere.config/vrj2.0a4
#cp ${VE_SUITE_HOME}/VE_Conductor/class_files/config/${HOST}.comm.config ${VE_SUITE_HOME}/VE_Conductor/class_files/config/comm.config
setenv OMNIORB_CONFIG ${MY_VJCONFIGS}/${HOST}.omniORB4.cfg
setenv OMNINAMES_LOGDIR ${OMNI_HOME}/logs

# VE_Suite needs vrjuggler to not find these files.
# The VES start script will rename the files by adding .bak
# The TApp253 start script will rename the files by removing the .bak
if ( -e ${VJ_BASE_DIR}/lib32/jccl/plugins/corba_rtrc.so ) then
mv ${VJ_BASE_DIR}/lib32/jccl/plugins/corba_rtrc.so ${VJ_BASE_DIR}/lib32/jccl/plugins/corba_rtrc.so.bak
echo "Hiding corba_rtrc.so"
endif
if ( -e ${VJ_BASE_DIR}/lib32/jccl/plugins/corba_rtrc_d.so ) then
mv ${VJ_BASE_DIR}/lib32/jccl/plugins/corba_rtrc_d.so ${VJ_BASE_DIR}/lib32/jccl/plugins/corba_rtrc_d.so.bak
echo "Hiding corba_rtrc_d.so"
endif

switch ($1)

  case -c:
    sgi_use_anyaddr \
    ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/project \
    ${MY_VJCONFIGS}/icecube.base.jconf \
    ${MY_VJCONFIGS}/icecube.displays.closed.jconf \
    ${MY_VJCONFIGS}/icecube.mstar.jconf \
    ${MY_VJCONFIGS}/icecube.rfwand.jconf 
  breaksw
  
  case -cf:
    sgi_use_anyaddr \
    ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/project \
    ${MY_VJCONFIGS}/icecube.base.jconf \
    ${MY_VJCONFIGS}/icecube.displays.closed.jconf \
    ${MY_VJCONFIGS}/icecube.mstar.headless.jconf \
    ${MY_VJCONFIGS}/icecube.rfwand.jconf 
  breaksw
  
  case -d:
    ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/project \
    ${MY_VJCONFIGS}/sim.base.jconf \
    ${MY_VJCONFIGS}/sim.wand.mixin.jconf 
  breaksw
  
  case -o:
    sgi_use_anyaddr \
    ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/project \
    ${MY_VJCONFIGS}/icecube.base.jconf \
    ${MY_VJCONFIGS}/icecube.displays.open.jconf \
    ${MY_VJCONFIGS}/icecube.mstar.jconf \
    ${MY_VJCONFIGS}/icecube.rfwand.jconf 
  breaksw
  
  case -of:
    sgi_use_anyaddr \
    ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/project \
    ${MY_VJCONFIGS}/icecube.base.jconf \
    ${MY_VJCONFIGS}/icecube.displays.open.jconf \
    ${MY_VJCONFIGS}/icecube.mstar.headless.jconf \
    ${MY_VJCONFIGS}/icecube.rfwand.jconf 
  breaksw
  
  case -nserv:
	killall omniNames
	rm ${OMNINAMES_LOGDIR}/omninames-${HOST}.log
	rm ${OMNINAMES_LOGDIR}/omninames-${HOST}.bak
    omniNames -start &
  breaksw
  
  case -menu:
    ve_conductor
  breaksw

  case -surface:
    ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/makeVtkSurface
  breaksw

  case -preproc:
    ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/preprocessor
  breaksw

  case -surf2stl:
    ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/convertSurfaceFileToStl
  breaksw

  case -transVTK:
    ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/transformVTK
  breaksw

  case -cfd2VTK:
    ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/translateToVtk
  breaksw

  case -make:
    cd ${VE_SUITE_HOME}/VE_Installer
    gmake
  breaksw
  
  case -makeclean:
    cd ${VE_SUITE_HOME}/VE_Installer
    gmake clean
    gmake cleandepend
  breaksw
  
  case -merge:
    ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/mergeVertices
  breaksw

  case -meshView:
    ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/meshViewer
  breaksw

  case -scalRange:
    ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/whatIsScalarRange
  breaksw

  case -doc:
        netscape file://${VE_SUITE_HOME}/docs/starcd/initial.htm &
  breaksw

  case -h:
  default:
  echo ""
  echo Usage:
  echo VES [ options ] 
  echo "         "-h = usage information
  echo The following indicates the workflow of getting the cfd data into the viewer.
  echo "   "-cfd2VTK = translate cfd flowdata to VTK format
  echo "     "-merge = merge vertices in unstructured grid
  echo "   "-preproc = precompute cutting planes
  echo This will start the viewer using the specified display
  echo "         "-c = cave display
  echo "        "-cf = cave display with fixed viewpoints
  echo "         "-d = desktop display
  echo "         "-o = wall display
  echo "        "-of = wall display with fixed viewpoints
  echo When the viewer has started, start the menu in another shell
  echo "      "-menu = start java menu to control viewer
  echo The following utilities might be useful occasionaly
  echo "     "-nserv = start corba server for menu
  echo "   "-surface = make VTK surface from fluid mesh
  echo "  "-surf2stl = convert surface file to stl
  echo "  "-transVTK = move flowdata to align with geometry
  echo "      "-make = make all VE-Suite modules
  echo " "-makeclean = clean all VE-Suite modules
  echo "     "-merge = merge vertices of vtk dataset
  echo "  "-meshView = view any vtk file
  echo " "-scalRange = view scalar range of any vtk file
  echo "       "-doc = view documentation for setting up a CFD post process case
  echo ""
  
endsw

