#/bin/csh -f
#copies the appropriate libs/dlls from ve-suite for installs 
#in the future cmd line args will specify VE-Suite home 

setenv VE_VERSION .0.9.3
#set vesuitedir $1
#if(vesuitedir)
#   setenv VE_SUITE_HOME $1
#else
#this is the release directory
if ( $?VE_SUITE_HOME ) then
   echo $VE_SUITE_HOME
else 
   setenv VE_SUITE_HOME /home/vr/Applications/TSVEG/VE_Suite_0.9.3/VE_Suite
endif

#determine the platform
if ( -e /etc/redhat-release ) then
   set firstWord=`cat /etc/redhat-release | awk -F" " '{print $1}'`
   #echo "firstWord is" ${firstWord}
   if ( ${firstWord} == "Red" ) then
      set thirdWord=`cat /etc/redhat-release | awk -F" " '{print $3}'`
      if ( ${thirdWord} == "Enterprise" ) then
         # extract some words from file to create something like RHEL_3
         setenv CFDHOSTTYPE `cat /etc/redhat-release | awk -F" " '{print "RHEL_" $7}'`
      else
         # extract some words from file to create something like RedHat_8.0 or RedHat_9
         setenv CFDHOSTTYPE `cat /etc/redhat-release | awk -F" " '{print $1 $2 "_" $5}'`
      endif
   else if ( ${firstWord} == "Fedora" ) then
      # extract some words from file to create something like Fedora_1
      setenv CFDHOSTTYPE `cat /etc/redhat-release | awk -F" " '{print $1 "_" $4}'`
   endif
else if ( -e /etc/SuSE-release ) then
   # extract some words from file to create something like SuSE_9.2_x86-64
   setenv CFDHOSTTYPE `head -1 /etc/SuSE-release | awk -F" " '{print $1 "_" $3 "_" $4}'`
else
   #echo "uname is" `uname`
   setenv CFDHOSTTYPE `uname`
endif

#if creation of CFDHOSTTYPE caused parenthesis to be inserted, then remove...
setenv CFDHOSTTYPE `echo \"$CFDHOSTTYPE\" | sed -e 's/(//g' | sed -e 's/)//g' | sed -e 's/"//g'`

#copy the appropriate dll's to the install directory

setenv VE_TEMP_INST_DIR ${VE_SUITE_HOME}/VE_Installer/unixLinuxInstalls
setenv VE_PLAT_INSTALL_DIR ${VE_SUITE_HOME}/VE_Installer/unixLinuxInstalls/${CFDHOSTTYPE}
setenv VE_INSTALLER_DIR ${VE_SUITE_HOME}/VE_Installer

echo "Copying libs/dlls for:" ${CFDHOSTTYPE}
echo "from:" ${VE_SUITE_HOME}
echo "to: " ${VE_PLAT_INSTALL_DIR}

#need to check somehow to see if the directory already exists
mkdir -p ${VE_PLAT_INSTALL_DIR}/bin
mkdir -p ${VE_PLAT_INSTALL_DIR}/exampleDatasets
#mkdir -p ${VE_PLAT_INSTALL_DIR}/exampleDatasets/simpleScalars/vectors
#mkdir -p ${VE_PLAT_INSTALL_DIR}/exampleDatasets/simpleScalars/scalars
#mkdir -p ${VE_PLAT_INSTALL_DIR}/glsl_shaders
#mkdir -p ${VE_PLAT_INSTALL_DIR}/images

echo "Removing old dlls and exes files..."
rm -rf ${VE_PLAT_INSTALL_DIR}/bin/*
echo "Removing old example dataset files..."
rm -rf ${VE_PLAT_INSTALL_DIR}/exampleDatasets/*
echo "Removing old shader files..."
rm -rf ${VE_PLAT_INSTALL_DIR}/glsl_shaders/*
#rm -rf ${VE_PLAT_INSTALL_DIR}/images/*

#go into the directory
cd ${VE_PLAT_INSTALL_DIR}

#copy the libs into the install dir

echo "Copying new exes..."
cp ${VE_SUITE_HOME}/bin/${CFDHOSTTYPE}/* ./bin

echo "Copying new dlls..."
cp ${VE_SUITE_HOME}/lib/${CFDHOSTTYPE}/* ./bin

echo "Copying example dataset..."
echo "   Sample VTK files..."
cp  ${VE_SUITE_HOME}/VE_TestSuite/2scl.vtk ./exampleDatasets/.
cp  ${VE_SUITE_HOME}/VE_TestSuite/3scl2vec.vtk ./exampleDatasets/.
cp  ${VE_SUITE_HOME}/VE_TestSuite/3scl.vtk ./exampleDatasets/.
cp  ${VE_SUITE_HOME}/VE_TestSuite/vtkPolyData.vtk ./exampleDatasets/.

echo "   Sample geometery files..."
cp ${VE_SUITE_HOME}/VE_TestSuite/eightCorners.stl ./exampleDatasets/.
cp ${VE_SUITE_HOME}/VE_TestSuite/Surface0.75.stl ./exampleDatasets/.

echo "   Sample texture data..."
cp -Rf ${VE_SUITE_HOME}/VE_TestSuite/simpleScalars ./exampleDatasets
#remove the subversion directory
rm -rf ./exampleDatasets/simpleScalars/.svn
rm -rf ./exampleDatasets/simpleScalars/scalars/.svn
rm -rf ./exampleDatasets/simpleScalars/vectors/.svn
cp ${VE_SUITE_HOME}/VE_TestSuite/*.txt ./exampleDatasets/.

echo "   Sample parameter file..."
cp -rf ${VE_SUITE_HOME}/VE_TestSuite/sswtexture.param ./exampleDatasets/sample.param

#echo "Copying glsl shaders..."
#cp -f ${VE_SUITE_HOME}/VE_TextureBased/glsl_shaders/*.glsl ./glsl_shaders/.

echo "Copying setup file..."
cp -f ${VE_SUITE_HOME}/VE_Installer/installer/setupUL.tsh ./setup.tsh

echo "Copying VES script..."
cp -f ${VE_SUITE_HOME}/VE_Installer/installer/VES.run ./bin/VES

echo "Copying README script..."
cp -f ${VE_SUITE_HOME}/VE_Installer/installer/README.unix.txt ./README.txt

echo "Creating tar.gz file..."
#write the tar file in the install directory
cd ${VE_TEMP_INST_DIR}
pwd

mkdir ./VE_Suite${VE_VERSION}
setenv VE_INSTALL_TAR_DIR ./VE_Suite${VE_VERSION}
mv ./${CFDHOSTTYPE} ${VE_INSTALL_TAR_DIR}

# on irix /usr/freeware/bin
if ( ${CFDHOSTTYPE} == "IRIX64" ) then
   /usr/freeware/bin/tar -zcvf ./vesuite_${VE_VERSION}_${CFDHOSTTYPE}.tar.gz ${VE_INSTALL_TAR_DIR}
else
   tar -zcvf ./vesuite_${VE_VERSION}_${CFDHOSTTYPE}.tar.gz ${VE_INSTALL_TAR_DIR}
endif

echo "Moving the tar.gz file to: "${VE_INSTALLER_DIR}
mv ./vesuite_${VE_VERSION}_${CFDHOSTTYPE}.tar.gz ${VE_INSTALLER_DIR}

echo "Removing temp install dir: " ${VE_TEMP_INST_DIR}
cd ${VE_INSTALLER_DIR}
pwd
rm -rf ${VE_TEMP_INST_DIR}

echo "Finished."
