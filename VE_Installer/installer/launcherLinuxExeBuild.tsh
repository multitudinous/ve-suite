#!/bin/csh
#Builds VE-Launcher as a standalone exe + static libraries for
#VE-Suite's Linux distribution.

#Settings for the environment.
#defaultVAR: VAR is set to defaultVAR if VAR doesn't have a value.
#locationVAR: locationVAR is appended to a ..PATH var. 
set osName=RHEL_4
set defaultCXPATH=/home/vr/Applications/TSVEG/Build_Apps/cx_Freeze-3.0.3
set locationCHRPATH=/home/vr/Applications/TSVEG/Build_Apps/chrpath-0.13/${osName}/bin
set defaultPYTHONHOME=/usr
set locationPYTHON=/usr/lib/python2.3

#Settings for the VE-Launcher executable.
set installDir=dist
set includeDir=python
set pathCorrectionScript=./soften

#CX_Freeze vars.
if ( ! $?CXPATH ) then
   setenv CXPATH ${defaultCXPATH}
endif
setenv PATH ${CXPATH}:${PATH}

#Python path vars.
if ( ! $?PYTHONHOME ) then
   setenv PYTHONHOME ${defaultPYTHONHOME}
endif

if ( $?PYTHONPATH ) then
   set tailPYTHONPATH=":${PYTHONPATH}"
else
   set tailPYTHONPATH=""
endif
setenv PYTHONPATH ${locationPYTHON}/site-packages:${locationPYTHON}${tailPYTHONPATH}

setenv PATH ${CXPATH}:${locationCHRPATH}:${PATH}

##TESTERS
echo CXPATH: ${CXPATH}
echo PYTHONHOME: ${PYTHONHOME}
echo PYTHONPATH: ${PYTHONPATH}
echo PATH: ${PATH}

#Remove old .pyc files, which can cause problems with the build.
rm -f *.pyc
set homeDir=`pwd`
cd ${includeDir}
rm -f *.pyc
cd ${homeDir}

#Build standalone exe.
FreezePython --install-dir=${installDir} --include-path=python --init-script=ConsoleSetLibPath velauncher.py

#Correct its libraries' paths.
${pathCorrectionScript} ${installDir}

echo "Finished building velauncher executable."
